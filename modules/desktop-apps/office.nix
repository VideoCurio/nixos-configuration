# Office suite desktop applications.

{ config, lib, pkgs, ... }:
let basecamp-cli = pkgs.callPackage ../../pkgs/basecamp-cli { };
in {
  # Declare options
  options = {
    curios.desktop.office = {
      enable = lib.mkOption {
        type = lib.types.bool;
        default = true;
        description = "Office applications - Obsidian, Joplin.";
      };
      calibre.enable = lib.mkOption {
        type = lib.types.bool;
        default = false;
        description = "Calibre e-books manager.";
      };
      evince.enable = lib.mkOption {
        type = lib.types.bool;
        default = true;
        description = "Evince document viewer for PDF and Postscript.";
      };
      libreoffice.enable = lib.mkOption {
        type = lib.types.bool;
        default = false;
        description = "LibreOffice suite desktop applications.";
      };
      onlyoffice.desktopeditors.enable = lib.mkOption {
        type = lib.types.bool;
        default = false;
        description = "OnlyOffice Desktop Editors suite.";
      };
      thunderbird.enable = lib.mkOption {
        type = lib.types.bool;
        default = false;
        description = "Mozilla Thunderbird email client.";
      };
      xournalpp.enable = lib.mkOption {
        type = lib.types.bool;
        default = true;
        description = "Xournal++ handwriting notetaking app with PDF support.";
      };
      crm = {
        salesforce = {
          enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = "Salesforce CRM web app.";
          };
          baseUrl = lib.mkOption {
            type = lib.types.str;
            default = "your-domain.my.salesforce.com";
            description = "Your Salesforce 'My Domain' base URL.";
          };
        };
        hubspot = {
          enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = "HubSpot CRM web app.";
          };
          baseUrl = lib.mkOption {
            type = lib.types.str;
            default = "app.hubspot.com";
            description = "HubSpot web app base URL.";
          };
        };
      };
      erp = {
        odoo = {
          enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = "Odoo ERP/CRM web app.";
          };
          baseUrl = lib.mkOption {
            type = lib.types.str;
            default = "mycompanynamehere.odoo.com";
            description = "Your Odoo server base URL.";
          };
        };
      };
      finance = {
        # TODO: find a pkgs worthy of installation: frappe books?
        gnucash.enable = lib.mkOption {
          type = lib.types.nullOr lib.types.bool;
          default = null;
          description = "DEPRECATED";
        };
      };
      ms = {
        office365 = {
          excel.enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = "Microsoft 365 Excel (buy online).";
          };
          powerpoint.enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = "Microsoft 365 PowerPoint (buy online).";
          };
          word.enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = "Microsoft 365 Word (buy online).";
          };
        };
      };
      projects = {
        basecamp = {
          enable = lib.mkOption {
            type = lib.types.bool;
            default = true;
            description = "Basecamp.com project management by 37signals.";
          };
          baseUrl = lib.mkOption {
            type = lib.types.str;
            default = "launchpad.37signals.com/signin";
            description = "Basecamp web app base URL.";
            example = "3.basecamp.com/0123456/";
          };
          cli = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = "Basecamp official command-line interface.";
          };
        };
        jira = {
          enable = lib.mkOption {
            type = lib.types.bool;
            default = false;
            description = "Atlassian Jira web-based project management.";
          };
          baseUrl = lib.mkOption {
            type = lib.types.nullOr lib.types.str;
            default = null;
            description =
              "Your Jira cloud custom domain like: mycompany.atlassian.net";
            example = "mycompanynamehere.atlassian.net";
          };
        };
      };
      conferencing = {
        slack.enable = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Slack webapp.";
        };
        teams.enable = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "MS Teams webapp.";
        };
        zoom.enable = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Zoom.us video conference app.";
        };
      };
    };
  };

  # Declare configuration
  config = lib.mkIf config.curios.desktop.office.enable {
    environment.systemPackages = [ pkgs.obsidian pkgs.joplin-desktop ]
      ++ lib.optionals config.curios.desktop.office.calibre.enable
      [ pkgs.calibre ]
      ++ lib.optionals config.curios.desktop.office.libreoffice.enable
      [ pkgs.libreoffice ] ++ lib.optionals
      (config.curios.desktop.office.onlyoffice.desktopeditors.enable
        && config.curios.platform.amd64.enable)
      [ pkgs.onlyoffice-desktopeditors ]
      ++ lib.optionals config.curios.desktop.office.thunderbird.enable
      [ pkgs.thunderbird ]
      ++ lib.optionals config.curios.desktop.office.xournalpp.enable
      [ pkgs.xournalpp ]
      ++ lib.optionals config.curios.desktop.office.crm.salesforce.enable
      [ (import ./webapp-salesforce.nix { inherit config pkgs lib; }) ]
      ++ lib.optionals config.curios.desktop.office.crm.hubspot.enable
      [ (import ./webapp-hubspot.nix { inherit config pkgs lib; }) ]
      ++ lib.optionals config.curios.desktop.office.erp.odoo.enable
      [ (import ./webapp-odoo.nix { inherit config pkgs lib; }) ]
      ++ lib.optionals config.curios.desktop.office.ms.office365.excel.enable
      [ (import ./webapp-ms-excel.nix { inherit pkgs lib; }) ] ++ lib.optionals
      config.curios.desktop.office.ms.office365.powerpoint.enable
      [ (import ./webapp-ms-powerpoint.nix { inherit pkgs lib; }) ]
      ++ lib.optionals config.curios.desktop.office.ms.office365.word.enable
      [ (import ./webapp-ms-word.nix { inherit pkgs lib; }) ]
      ++ lib.optionals config.curios.desktop.office.projects.basecamp.enable
      [ (import ./webapp-basecamp.nix { inherit config pkgs lib; }) ]
      ++ lib.optionals config.curios.desktop.office.projects.basecamp.cli
      [ basecamp-cli ]
      ++ lib.optionals config.curios.desktop.office.projects.jira.enable
      [ (import ./webapp-jira.nix { inherit config pkgs lib; }) ]
      ++ lib.optionals config.curios.desktop.office.conferencing.slack.enable
      [ (import ./webapp-slack.nix) ]
      ++ lib.optionals config.curios.desktop.office.conferencing.teams.enable
      [ (import ./webapp-teams.nix) ] ++ lib.optionals
      (config.curios.desktop.office.conferencing.zoom.enable
        && config.curios.platform.amd64.enable) [ pkgs.zoom-us ];

    programs = {
      evince.enable = lib.mkDefault config.curios.desktop.office.evince.enable;
    };
  };
}
